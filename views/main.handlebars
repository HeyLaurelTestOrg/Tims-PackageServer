<xsl:stylesheet xmlns:ns="http://www.woltlab.com" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" version="1.0">
	<xsl:output method="html" doctype-system="about:legacy-compat"/>
	
	<xsl:template match="/">
		<html>
			<head>
				<meta charset="utf-8" />
				<meta http-equiv="X-UA-Compatible" content="IE=edge" />
				<meta name="viewport" content="width=device-width, initial-scale=1" />
				
				<!-- <link rel="icon" href="favicon.ico" /> -->
				
				<link rel="stylesheet" href="//cdnjs.cloudflare.com/ajax/libs/twitter-bootstrap/3.2.0/css/bootstrap.min.css" />
				
				<style>
					body {
						position: relative;
					}
					
					#main, .sidebar {
						margin-top: 50px;
					}
					
					#main {
						padding: 40px 15px;
					}
					
					.main > .row {
						poisition: relative;
					}
					
					#sidebarNav {
						padding-top: 40px;
						padding-bottom: 40px;
					}
					
					.sidebar {
						overflow-y: auto;
						position: fixed;
						top: 0;
						left: 0;
						bottom: 0;
					}
					
					.jsOnly {
						display: none;
					}
					
					a.authorURL {
						text-decoration: underline;
					}
					
					a.authorURL:hover {
						text-decoration: none;
					}
					
					#noResults {
						display: none;
					}
					
					/* http://nicolasgallagher.com/jump-links-and-viewport-positioning/demo/#method-C */
					.anchorFix {
						padding-top: 60px;
						margin-top: -60px;
						-webkit-background-clip: content-box;
						background-clip: content-box;
					}
				</style>
				
				<title>{{title}}</title>
			</head>
			<body>
				<div class="navbar navbar-inverse navbar-fixed-top hidden-print" role="navigation">
					<div class="container-fluid">
						<div class="navbar-header">
							<button type="button" class="navbar-toggle collapsed" data-toggle="collapse" data-target=".navbar-collapse">
								<span class="sr-only">Toggle navigation</span>
								<span class="icon-bar"></span>
								<span class="icon-bar"></span>
								<span class="icon-bar"></span>
							</button>
							<a class="navbar-brand" href="#">{{title}}</a>
						</div>
						<div class="navbar-collapse collapse">
							<ul class="nav navbar-nav navbar-right">
								<li><a href="https://github.com/wbbaddons/Tims-PackageServer">Tim’s PackageServer on GitHub</a></li>
							</ul>
							
							<div class="navbar-form navbar-right jsOnly">
								<input id="search" type="text" class="form-control" maxlength="32" placeholder="{{lang.search}}" />
							</div>
						</div>
					</div>
				</div>
				
				<div class="main container-fluid">
					<div class="row">
						<div class="col-md-2 sidebar hidden-xs hidden-sm hidden-print">
							<div id="sidebarNav">
								<ul class="nav nav-pills nav-stacked">
									<xsl:for-each select="ns:section/ns:package">
										<xsl:sort select="ns:packageinformation/ns:packagename" />
										
										<li class="jsPackage">
											<xsl:attribute name="id">nav-<xsl:value-of select="translate(@name, '.', '-')" /></xsl:attribute>
											
											<a>
												<xsl:attribute name="href">#<xsl:value-of select="translate(@name, '.', '-')" /></xsl:attribute>
												
												<xsl:value-of select="./ns:packageinformation/ns:packagename" />
											</a>
										</li>
									</xsl:for-each>
								</ul>
							</div>
						</div>
						
						<div id="main" class="col-md-10 col-md-offset-2" role="main">
							<xsl:for-each select="ns:section/ns:package">
								<xsl:sort select="ns:packageinformation/ns:packagename" />
								
								<div class="jsPackage anchorFix">
									<xsl:attribute name="id"><xsl:value-of select="translate(@name, '.', '-')" /></xsl:attribute>
									
									<div class="panel panel-default packagePanel">
										<xsl:if test="ns:packageinformation/ns:isapplication='1'">
											<xsl:attribute name="class">panel panel-primary packagePanel</xsl:attribute>
										</xsl:if>
										
										<div class="panel-heading">
											<h3 class="panel-title">
												<xsl:value-of select="ns:packageinformation/ns:packagename" /> (<xsl:value-of select="@name" />)
												
												<xsl:if test="ns:authorinformation">
													<xsl:choose>
														<xsl:when test="ns:authorinformation/ns:authorurl">
															{{lang.by}}
															<a class="authorURL">
																<xsl:attribute name="href"><xsl:value-of select="ns:authorinformation/ns:authorurl" /></xsl:attribute>
																
																<xsl:choose>
																	<xsl:when test="ns:authorinformation/ns:author">
																		<xsl:value-of select="ns:authorinformation/ns:author" />
																	</xsl:when>
																	<xsl:otherwise>
																		<xsl:value-of select="ns:authorinformation/ns:authorurl" />
																	</xsl:otherwise>
																</xsl:choose>
															</a>
														</xsl:when>
														<xsl:otherwise>
															<xsl:if test="ns:authorinformation/ns:author">
																{{lang.by}} <xsl:value-of select="ns:authorinformation/ns:author" />
															</xsl:if>
														</xsl:otherwise>
													</xsl:choose>
												</xsl:if>
											</h3>
										</div>
										<xsl:if test="ns:packageinformation/ns:packagedescription!=''">
											<div class="panel-body">
												<xsl:value-of select="ns:packageinformation/ns:packagedescription" />
											</div>
										</xsl:if>
										<div class="table-responsive">
											<table class="table table-striped">
												<thead>
													<tr>
														<th class="col-md-2">{{lang.version}}</th>
														<th class="col-md-4">{{lang.license}}</th>
														<th class="col-md-4">{{lang.requirements}}</th>
														<th class="col-md-2 jsOnly">{{lang.date}}</th>
													</tr>
												</thead>
												<tbody>
													<xsl:for-each select="ns:versions/ns:version">
														<xsl:sort select="position()" data-type="number" order="descending" />
														
														<tr>
															<td>
																<!-- Latest version anchor -->
																<xsl:if test="position()=1">
																	<span class="anchorFix">
																		<xsl:attribute name="id"><xsl:value-of select="translate(translate(../../@name, ' ', '-'), '.', '-')" />-latest</xsl:attribute>
																	</span>
																</xsl:if>
																
																<!-- Version anchor -->
																<span class="anchorFix">
																	<xsl:attribute name="id"><xsl:value-of select="translate(translate(../../@name, ' ', '-'), '.', '-')" />-<xsl:value-of select="translate(translate(@name, ' ', '-'), '.', '-')" /></xsl:attribute>
																</span>
																
																<xsl:choose>
																	<xsl:when test="@accessible='true'">
																		<a>
																			<xsl:attribute name="href"><xsl:value-of select="ns:file" /></xsl:attribute>
																			
																			<xsl:value-of select="@name" />
																		</a>
																	</xsl:when>
																	<xsl:otherwise>
																		<xsl:value-of select="@name" />
																	</xsl:otherwise>
																</xsl:choose>
															</td>
															<td>
																<xsl:choose>
																	<xsl:when test="ns:license">
																		<xsl:choose>
																			<xsl:when test="ns:license/@url">
																				<a>
																					<xsl:attribute name="href"><xsl:value-of select="ns:license/@url" /></xsl:attribute>
																					<xsl:value-of select="ns:license" />
																				</a>
																			</xsl:when>
																			<xsl:otherwise>
																				<xsl:value-of select="ns:license" />
																			</xsl:otherwise>
																		</xsl:choose>
																	</xsl:when>
																	<xsl:otherwise>{{lang.missingLicenseInformation}}</xsl:otherwise>
																</xsl:choose>
															</td>
															<td>
																<xsl:choose>
																	<xsl:when test="ns:requiredpackages/ns:requiredpackage">
																		<ul>
																			<xsl:for-each select="ns:requiredpackages/ns:requiredpackage">
																				<xsl:choose>
																					<xsl:when test="/ns:section/ns:package[@name=current()]">
																						<li>
																							<a>
																								<xsl:attribute name="href">
																									#<xsl:value-of select="translate(., '.', '-')" />
																								</xsl:attribute>
																								
																								<xsl:value-of select="." /> (<xsl:value-of select="@minversion" />)
																							</a>
																						</li>
																					</xsl:when>
																					<xsl:otherwise>
																						<li>
																							<xsl:value-of select="." /> (<xsl:value-of select="@minversion" />)
																						</li>
																					</xsl:otherwise>
																				</xsl:choose>
																			</xsl:for-each>
																		</ul>
																	</xsl:when>
																	<xsl:otherwise>{{lang.noRequirements}}</xsl:otherwise>
																</xsl:choose>
															</td>
															<td class="jsOnly">
																<time>
																	<xsl:value-of select="ns:timestamp" />
																</time>
															</td>
														</tr>
													</xsl:for-each>
												</tbody>
											</table>
										</div>
									</div>
								</div>
							</xsl:for-each>
							
							<div id="noResults" class="panel panel-warning">
								<div class="panel-heading">We’ve come up empty ;(</div>
								<div class="panel-body">
									No packages found that match your search.
								</div>
							</div>
							
							<div id="timsPackageServerLicenseInfo" class="anchorFix">
								<div class="panel panel-default">
									<div class="panel-body">
										<p>This list was presented by <a href="https://github.com/wbbaddons/Tims-PackageServer">Tim’s PackageServer</a> in version {{serverVersion}}.</p>
										<p>Tim’s PackageServer is licensed under the terms of the <a href="https://www.gnu.org/licenses/agpl-3.0.html">GNU Affero General Public License v3</a>.</p>
										<p>Click <a href="{{host}}/app.coffee">here</a> to obtain a copy of the source code of this installation.</p>
									</div>
								</div>
							</div>
						</div>
					</div>
				</div>
				
				<script src="//cdnjs.cloudflare.com/ajax/libs/jquery/2.1.1/jquery.min.js"></script>
				<script src="//cdnjs.cloudflare.com/ajax/libs/moment.js/2.8.3/moment-with-locales.min.js"></script>
				<script src="//cdnjs.cloudflare.com/ajax/libs/twitter-bootstrap/3.2.0/js/bootstrap.min.js"></script>
				<script src="//cdnjs.cloudflare.com/ajax/libs/fuse.js/1.2.0/fuse.min.js"></script>
				
				<script>
					window.packageSearch = [
						<xsl:for-each select="ns:section/ns:package">
							{
								id: '<xsl:value-of select="@name" />',
								name: '<xsl:call-template name="escapeQuotes"><xsl:with-param name="txt" select="ns:packageinformation/ns:packagename" /></xsl:call-template>',
								description: '<xsl:call-template name="escapeQuotes"><xsl:with-param name="txt" select="ns:packageinformation/ns:packagedescription" /></xsl:call-template>',
								author: '<xsl:call-template name="escapeQuotes"><xsl:with-param name="txt" select="ns:authorinformation/ns:author" /></xsl:call-template>',
								authorURL: '<xsl:call-template name="escapeQuotes"><xsl:with-param name="txt" select="ns:authorinformation/ns:authorurl" /></xsl:call-template>'
							},
						</xsl:for-each>
					];
				</script>
				
				<script>
					$(document).ready(function () {
						var fuse = new Fuse(packageSearch, {
							keys: ['name', 'description'],
							includeScore: true,
							threshold: 0.5,
							distance: 100
						});
						
						window.packageSearch = null; // Free some memory, hopefully
						
						requestAnimationFrame = (function() {
							if ("requestAnimationFrame" in window) {
								return window.requestAnimationFrame;
							}
							else {
								return function(call) {
									if (typeof call === 'function') {
										call();
									}
								}
							}
						})();
						
						(function(window) {
							var defaultOrder = (function() {
								var list = [];
								
								$('#main .jsPackage').each(function() {
									list.push($(this).attr('id'));
								});
								
								return list;
							})();
							
							var sortDOMElements = function(container, selector, order) {
								order = order || defaultOrder;
								
								var elements = [];
								
								$.each(order, function(index, value) {
									elements.push($(selector + value));
								});
								
								container.prepend(elements);
							};
							
							_doSearch = function(value) {
								value = value.substr(0, 32);
								
								if (!value.length) {
									sortDOMElements($('#main'), '#');
									sortDOMElements($('#sidebarNav > ul'), '#nav-');
									
									$('.jsPackage').show();
									$('#noResults').hide();
									
									$('body').scrollspy('refresh');
									$('body').trigger('scroll');
									
									return;
								}
								
								var results = fuse.search(value);
								
								results = results.map(function(v, k, a) {
									return v.item.id.replace(/\./g, '-');
								});
								
								if (results.length) {
									$('.jsPackage').hide();
									
									sortDOMElements($('#main'), '#', results);
									sortDOMElements($('#sidebarNav > ul'), '#nav-', results);
									
									$('#' + results.join(', #')).show();
									$('#nav-' + results.join(', #nav-')).show();
									
									$('#noResults').hide();
								}
								else {
									$('.jsPackage').hide();
									$('#noResults').show();
								}
								
								$('body').scrollspy('refresh');
								$('body').trigger('scroll');
							};
							
							window.doSearch = function(value) {
								requestAnimationFrame(function() {
									_doSearch(value, false);
									$(window).scrollTop(0);
								});
							};
						})(window);
						
						$('#search').on('input change', function() {
							doSearch($(this).val());
						});
						
						moment.locale(window.navigator.userLanguage || window.navigator.language);
						
						$('time').each(function(k, v) {
							$(v).html(moment.unix($(v).text()).format('LL'));
						});
						
						$('.jsOnly').show();
						
						$('body').scrollspy({
							target: "#sidebarNav",
							offset: 50
						});
						
						var sidebar = $('#sidebarNav').parent();
						
						$('ul.nav li').on('activate.bs.scrollspy', function () {
							siblingsHeight = (function() {
								height = 0;
								$(this).prevAll().each(function(k, v) {
									height += $(v).outerHeight(true);
								});
								
								return height;
							}).call(this);
							
							sidebar.scrollTop(siblingsHeight);
						});
					});
				</script>
			</body>
		</html>
	</xsl:template>
	
	<xsl:template name="escapeQuotes">
		<!-- http://mac-blog.org.ua/xslt-escape-single-quotes/ -->
		
		<xsl:param name="txt"/>
		<xsl:variable name="backSlashQuote">&#92;&#39;</xsl:variable>
		<xsl:variable name="singleQuote">&#39;</xsl:variable>
		
		<xsl:choose>
			<xsl:when test="string-length($txt) = 0">
				<!-- early return -->
			</xsl:when>
			
			<xsl:when test="contains($txt, $singleQuote)">
				<xsl:value-of disable-output-escaping="yes" select="concat(substring-before($txt, $singleQuote), $backSlashQuote)"/>
				
				<xsl:call-template name="escapeQuotes">
					<xsl:with-param name="txt" select="substring-after($txt, $singleQuote)"/>
				</xsl:call-template>
			</xsl:when>
			
			<xsl:otherwise>
				<xsl:value-of disable-output-escaping="yes" select="$txt"/>
			</xsl:otherwise>
		</xsl:choose>
	</xsl:template>
</xsl:stylesheet>